plugins {
    id 'java'
    id 'antlr'
    id 'maven-publish'
}

group 'i5.acis'
version '0.1-SNAPSHOT'

sourceCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    antlr "org.antlr:antlr4:4.11.1"
    implementation "org.antlr:antlr4-runtime:4.11.1"
    implementation 'com.github.javaparser:javaparser-core:3.24.7'
}

test {
    useJUnitPlatform()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"

    // Keep a copy of generated sources
    doLast {
        println "Copying generated grammar lexer/parser files to main directory."
        copy {
            from "${buildDir}/generated-src/antlr/main"
            into "generated-src/main/java/generated-parser"
        }
    }
}

clean.doLast {
    file('generated-src').deleteDir()
}

// Publish to Maven Repository
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
