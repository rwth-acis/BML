Bot() {
    OpenAPI mensaAPI(url="https://marcluque.com/openmensa.json")

    Rasa rasa(url="https://rasa3.tech4comp.dbis.rwth-aachen.de", trainingFile="env:MENSA_TRAINING_DATA_FILE")

    Telegram telegram(botToken="env:TELEGRAM_BOT_TOKEN", botName="TestBMLBot")

    Slack slack(botToken="env:SLACK_BOT_TOKEN", appToken="env:SLACK_APP_TOKEN")

    Dialogue mensaDialogue() {
        @Action getMenu(context) {
            today = date(format="yyyy-MM-dd")
            meals = mensaAPI.get(path="/canteens/{id}/days/{date}/meals", id=187, date=today)
            if meals.code != 200 {
                send(text="I'm sorry, it seems like something went wrong while retrieving the menu for today ðŸ˜”")
            } else {
                menuResponse = "*TODAY'S MENU (" + today + ") for RWTH Mensa Academica*\n\n"
                sides = "_Sides_\n"
                forEach m in meals {
                    if m.category != "Hauptbeilagen" && m.category != "Nebenbeilage" {
                        menuResponse += "_" + m.category + "_ - " + string(number=m.prices.students) + "â‚¬\n"
                        menuResponse += m.name + "\n"
                        menuResponse += "_Notes_: " + m.notes.join(delimiter=", ") + "\n\n"
                    } else {
                        sides += m.name + "\n"
                    }
                }
                send(text=menuResponse + sides)
            }
        }

        default(action="Iâ€™m sorry, I didn't understand that, come again please.")

        sink(intent="bye, stop", action="Until next time!")
        sink(intent="thanks", action="You're welcome!")

        initial(intent="_", action="I'm not sure how I can be of your assistance. All I can do for you is retrieve the RWTH Academica mensa menu")
        greetings = ["Hi there, fellow human! ðŸ¤–", "Hi, nice to meet you!", "Hello, pleasure to meet you!", "Hi there! ðŸ‘‹"]
        initial(intent="greet", action=greetings)
        initial(intent="functions, options", action="I'm a simple mensa bot, I can retrieve today's RWTH Academica mensa menu")
        initial(intent="menu", action=getMenu)
    }

    @UserStartedChat @UserSentMessage @UserLeftChat handleMensaDialogue(ctx) {
        mensaDialogue.step(context=ctx)
    }
}