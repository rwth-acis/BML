Bot(host="localhost", port=8080) {

    OpenAPI petStore(url="https://petstore3.swagger.io/api/v3/openapi.json")

    Rasa rasa(url="https://rasa3.tech4comp.dbis.rwth-aachen.de/")

    Telegram telegram(botName="TestBMLBot", token="myTelegramToken")

    Slack slack(token="mySlackToken")

    Dialogue pizzaScarfReview() {
        @Action
        postReview(context) {
            review = {name = context.entity, photoUrls = ["someUrl"]}

            response = petStore.post(path="/pet", body=review)

            if response.code == 200 {
                send(text="Your review was successfully processed!")
            } else {
                send(text="I'm sorry, it seems like there is a problem on our side. Please try again later.")
            }
        }

        @Action
        verifyReceiptId(context) {
            response = petStore.get(path="/pet/{petId}", petId=number(string=context.entity))
            if response.code != 200 {
                send(text="I'm sorry, you didn't provide a valid receipt id.")
            }
        }

        greetings = ["Hi there, fellow human!", "Hi, nice to meet you!", "Hello, a pleasure to meet you!"]

        notUnderstood = default(intent="_", action="Iâ€™m sorry, I didn't understand that, come again please.")

        greet = initial(intent="hello, greet", action=greetings)
        options = initial(intent="functions, options", action="I'm a simple rating bot, I can submit ratings for you.")

        bye = sink(intent="bye", action="Until next time!")
        stop = sink(intent="stop", action="Until next time!")

        addReview = initial(intent="review", action="Please provide your receipt ID. You can find it lorem ipsum ...")
        giveRating = state(action="How many stars would you like to give?")
        repeat = state(action="Would you like to make another review?")

        addReview -> [
            state(intent="receiptId", action=verifyReceiptId) -> giveRating,
            state(intent="reject", action="Sorry , I didn't get that. Please specify your receipt ID.") -> state(action=jumpTo(state=addReview))
        ]

        giveRating -> [
            state(intent="stars", action=postReview) -> [
                state(intent="yes", action=jumpTo(state=addReview)),
                state(intent="no", action="Okay, thank you. We're looking forward to your next visit!")
            ],
            state(intent="reject", action="Sorry , I didn't get that. Please specify how many stars you would like to give.") -> giveRating
        ]
    }

    @UserSentMessage
    @UserStartedChat
    messageHandler(context) {
        pizzaScarfReview.step(context=context);
    }
}
